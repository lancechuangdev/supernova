// SPDX-License-Identifier: MIT

pragma solidity ^0.8.24;

// Asset tokens for lending and borrowing, for example:
// lendToken: USDT / USDC / BUSD
// borrowToken: WBTC / WETH / DAI
interface IERC20Like {
    function approve(address spender, uint256 amount) external returns (bool);
    function transfer(address to, uint256 amount) external returns (bool);
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}

// Protocol receipt tokens for lenders and borrowers
interface IDebtTokenLike {
    function burn(address from, uint256 amount) external returns (bool);
    function mint(address to, uint256 amount) external returns (bool);
}

interface IOracleLike {
    function getPrice(address asset) external view returns (uint256);
}

interface IDexRouterLike {
    function getAmountIn(address tokenIn, address tokenOut, uint256 amountOut) external view returns (uint256);
    function getAmountOut(address tokenIn, address tokenOut, uint256 amountIn) external view returns (uint256);
    function swapExactTokensForTokens(
        address tokenIn,
        address tokenOut,
        uint256 amountIn,
        uint256 amountOutMin,
        address recipient
    ) external returns (uint256 amountOut);
    function swapTokensForExactTokens(
        address tokenIn,
        address tokenOut,
        uint256 amountOut,
        uint256 amountInMax,
        address recipient
    ) external returns (uint256 amountIn);
}

contract LearningPledgePool {
    uint256 private constant RATE_BASE = 1e8;
    uint256 private constant PRICE_SCALE = 1e18;
    uint256 private constant SECONDS_PER_YEAR = 365 days; // days is a Solidity time unit, it converts to seconds at compile time

    enum PoolState {
        MATCH,
        EXECUTION,
        REPAID,
        LIQUIDATION,
        UNDONE
    }

    struct CreatePoolParams {
        uint256 settleTime;
        uint256 endTime;
        uint256 interestRate;
        uint256 maxSupply;
        uint256 mortgageRate;
        address lendToken;
        address borrowToken;
        address spToken;
        address jpToken;
        uint256 autoLiquidateThreshold;
    }

    struct PoolBaseInfo {
        uint256 settleTime;
        uint256 endTime;
        uint256 interestRate;
        uint256 maxSupply;
        uint256 lendSupply;
        uint256 borrowSupply;
        uint256 mortgageRate;
        address lendToken;
        address borrowToken;
        PoolState state;
        address spToken;
        address jpToken;
        uint256 autoLiquidateThreshold;
    }

    struct PoolDataInfo {
        uint256 settleAmountLend; // required lend amount
        uint256 settleAmountBorrow; // required collateral amount
        uint256 finishAmountLend;
        uint256 finishAmountBorrow;
        uint256 liquidationAmountLend;
        uint256 liquidationAmountBorrow;
    }

    struct LendInfo {
        uint256 stakeAmount;
        uint256 refundAmount;
        bool hasRefunded;
        bool hasClaimed;
    }

    struct BorrowInfo {
        uint256 stakeAmount;
        uint256 refundAmount;
        bool hasRefunded;
        bool hasClaimed;
    }

    address public owner;
    address public oracle;
    address public dexRouter;
    address payable public feeAddress;
    bool public globalPaused;
    uint256 public minLendAmount = 100 ether;
    uint256 public minBorrowAmount = 1 ether;

    PoolBaseInfo[] private pools;
    PoolDataInfo[] private poolData;
    mapping(address => mapping(uint256 => LendInfo)) public userLendInfo;
    mapping(address => mapping(uint256 => BorrowInfo)) public userBorrowInfo;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
    event PoolCreated(
        uint256 indexed poolId,
        address indexed lendToken,
        address indexed borrowToken,
        address spToken,
        address jpToken,
        uint256 settleTime,
        uint256 endTime
    );
    event FeeAddressUpdated(address indexed previousFeeAddress, address indexed newFeeAddress);
    event DexRouterUpdated(address indexed previousDexRouter, address indexed newDexRouter);
    event MinLendAmountUpdated(uint256 previousMinAmount, uint256 newMinAmount);
    event MinBorrowAmountUpdated(uint256 previousMinAmount, uint256 newMinAmount);
    event PauseUpdated(bool paused);
    event DepositLend(address indexed lender, uint256 indexed poolId, address indexed token, uint256 amount);
    event DepositBorrow(address indexed borrower, uint256 indexed poolId, address indexed token, uint256 amount);
    event RefundLend(address indexed lender, uint256 indexed poolId, address indexed token, uint256 amount);
    event RefundBorrow(address indexed borrower, uint256 indexed poolId, address indexed token, uint256 amount);
    event ClaimLend(address indexed lender, uint256 indexed poolId, address indexed spToken, uint256 spAmount);
    event ClaimBorrow(
        address indexed borrower,
        uint256 indexed poolId,
        address indexed jpToken,
        uint256 jpAmount,
        uint256 loanAmount
    );
    event PoolRepaid(
        uint256 indexed poolId,
        address indexed router,
        uint256 collateralSold,
        uint256 repaymentAmount,
        uint256 remainingCollateralAmount
    );
    event PoolLiquidated(
        uint256 indexed poolId,
        address indexed router,
        uint256 collateralSold,
        uint256 lendTokenRecovered,
        uint256 remainingCollateralAmount
    );
    event WithdrawLend(address indexed lender, uint256 indexed poolId, uint256 spAmount, uint256 lendAmount);
    event WithdrawBorrow(address indexed borrower, uint256 indexed poolId, uint256 jpAmount, uint256 collateralAmount);
    event StateChanged(uint256 indexed poolId, PoolState previousState, PoolState newState);

    constructor(address oracle_, address payable feeAddress_) {
        require(oracle_ != address(0), "LearningPledgePool: zero oracle");
        require(feeAddress_ != address(0), "LearningPledgePool: zero fee address");

        owner = msg.sender;
        oracle = oracle_;
        feeAddress = feeAddress_;

        emit OwnershipTransferred(address(0), msg.sender);
    }

    function createPool(CreatePoolParams calldata params) external onlyOwner returns (uint256 poolId) {
        require(params.settleTime > block.timestamp, "LearningPledgePool: settle time not future");
        require(params.endTime > params.settleTime, "LearningPledgePool: end before settle");
        require(params.maxSupply > 0, "LearningPledgePool: zero max supply");
        require(params.interestRate > 0, "LearningPledgePool: zero interest rate");
        require(params.mortgageRate > 0, "LearningPledgePool: zero mortgage rate");
        require(params.lendToken != address(0), "LearningPledgePool: zero lend token");
        require(params.borrowToken != address(0), "LearningPledgePool: zero borrow token");
        require(params.lendToken != params.borrowToken, "LearningPledgePool: same pool tokens");
        require(params.spToken != address(0), "LearningPledgePool: zero sp token");
        require(params.jpToken != address(0), "LearningPledgePool: zero jp token");
        require(params.spToken != params.jpToken, "LearningPledgePool: same debt tokens");

        poolId = pools.length;

        pools.push(
            PoolBaseInfo({
                settleTime: params.settleTime,
                endTime: params.endTime,
                interestRate: params.interestRate,
                maxSupply: params.maxSupply,
                lendSupply: 0,
                borrowSupply: 0,
                mortgageRate: params.mortgageRate,
                lendToken: params.lendToken,
                borrowToken: params.borrowToken,
                state: PoolState.MATCH,
                spToken: params.spToken,
                jpToken: params.jpToken,
                autoLiquidateThreshold: params.autoLiquidateThreshold
            })
        );
        poolData.push(
            PoolDataInfo({
                settleAmountLend: 0,
                settleAmountBorrow: 0,
                finishAmountLend: 0,
                finishAmountBorrow: 0,
                liquidationAmountLend: 0,
                liquidationAmountBorrow: 0
            })
        );

        emit PoolCreated(
            poolId,
            params.lendToken,
            params.borrowToken,
            params.spToken,
            params.jpToken,
            params.settleTime,
            params.endTime
        );
    }

    function poolLength() external view returns (uint256) {
        return pools.length;
    }

    function getPool(uint256 poolId) external view poolExists(poolId) returns (PoolBaseInfo memory) {
        return pools[poolId];
    }

    function getPoolData(uint256 poolId) external view poolExists(poolId) returns (PoolDataInfo memory) {
        return poolData[poolId];
    }

    function getPoolState(uint256 poolId) external view poolExists(poolId) returns (PoolState) {
        return pools[poolId].state;
    }

    function isBeforeSettle(uint256 poolId) external view poolExists(poolId) returns (bool) {
        return block.timestamp < pools[poolId].settleTime;
    }

    function getRequiredRepayment(uint256 poolId) public view poolExists(poolId) returns (uint256) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];

        uint256 term = pool.endTime - pool.settleTime;
        uint256 interest = (data.settleAmountLend * pool.interestRate * term) / (RATE_BASE * SECONDS_PER_YEAR);

        return data.settleAmountLend + interest;
    }

    function isLiquidatable(uint256 poolId) public view poolExists(poolId) returns (bool) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];

        if (pool.state != PoolState.EXECUTION || data.settleAmountLend == 0 || data.settleAmountBorrow == 0) {
            return false;
        }

        uint256 lendPrice = IOracleLike(oracle).getPrice(pool.lendToken);
        uint256 borrowPrice = IOracleLike(oracle).getPrice(pool.borrowToken);
        require(lendPrice > 0, "LearningPledgePool: missing lend price");
        require(borrowPrice > 0, "LearningPledgePool: missing borrow price");

        uint256 borrowToLendRatio = (borrowPrice * PRICE_SCALE) / lendPrice;
        uint256 collateralValueInLend = (data.settleAmountBorrow * borrowToLendRatio) / PRICE_SCALE;
        uint256 liquidationThreshold = (data.settleAmountLend * (RATE_BASE + pool.autoLiquidateThreshold)) / RATE_BASE;

        return collateralValueInLend < liquidationThreshold;
    }

    function depositLend(uint256 poolId, uint256 amount)
        external
        whenNotPaused
        poolExists(poolId)
        stateMatch(poolId)
        beforeSettle(poolId)
    {
        PoolBaseInfo storage pool = pools[poolId];
        LendInfo storage lendInfo = userLendInfo[msg.sender][poolId];

        require(amount >= minLendAmount, "LearningPledgePool: lend amount too small");
        require(pool.lendSupply + amount <= pool.maxSupply, "LearningPledgePool: lend supply exceeded");

        bool success = IERC20Like(pool.lendToken).transferFrom(msg.sender, address(this), amount);
        require(success, "LearningPledgePool: lend transfer failed");

        lendInfo.stakeAmount += amount;
        lendInfo.hasRefunded = false;
        lendInfo.hasClaimed = false;
        pool.lendSupply += amount;

        emit DepositLend(msg.sender, poolId, pool.lendToken, amount);
    }

    function depositBorrow(uint256 poolId, uint256 amount)
        external
        whenNotPaused
        poolExists(poolId)
        stateMatch(poolId)
        beforeSettle(poolId)
    {
        PoolBaseInfo storage pool = pools[poolId];
        BorrowInfo storage borrowInfo = userBorrowInfo[msg.sender][poolId];

        require(amount >= minBorrowAmount, "LearningPledgePool: borrow amount too small");

        bool success = IERC20Like(pool.borrowToken).transferFrom(msg.sender, address(this), amount);
        require(success, "LearningPledgePool: borrow transfer failed");

        borrowInfo.stakeAmount += amount;
        borrowInfo.hasRefunded = false;
        borrowInfo.hasClaimed = false;
        pool.borrowSupply += amount;

        emit DepositBorrow(msg.sender, poolId, pool.borrowToken, amount);
    }

    function settle(uint256 poolId) external onlyOwner poolExists(poolId) stateMatch(poolId) afterSettle(poolId) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];

        if (pool.lendSupply == 0 || pool.borrowSupply == 0) {
            data.settleAmountLend = pool.lendSupply;
            data.settleAmountBorrow = pool.borrowSupply;
            _setPoolState(poolId, PoolState.UNDONE);
            return;
        }

        uint256 lendPrice = IOracleLike(oracle).getPrice(pool.lendToken);
        uint256 borrowPrice = IOracleLike(oracle).getPrice(pool.borrowToken);
        require(lendPrice > 0, "LearningPledgePool: missing lend price");
        require(borrowPrice > 0, "LearningPledgePool: missing borrow price");

        uint256 borrowToLendRatio = (borrowPrice * PRICE_SCALE) / lendPrice;
        uint256 collateralValueInLend = (pool.borrowSupply * borrowToLendRatio) / PRICE_SCALE;
        uint256 maxSettleLend = (collateralValueInLend * RATE_BASE) / pool.mortgageRate;

        if (pool.lendSupply > maxSettleLend) {
            data.settleAmountLend = maxSettleLend;
            data.settleAmountBorrow = pool.borrowSupply;
        } else {
            data.settleAmountLend = pool.lendSupply;
            data.settleAmountBorrow = (pool.lendSupply * pool.mortgageRate * lendPrice) / (borrowPrice * RATE_BASE);
        }

        _setPoolState(poolId, PoolState.EXECUTION);
    }

    function refundLend(uint256 poolId) external whenNotPaused poolExists(poolId) stateExecution(poolId) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];
        LendInfo storage lendInfo = userLendInfo[msg.sender][poolId];

        require(lendInfo.stakeAmount > 0, "LearningPledgePool: no lend stake");
        require(!lendInfo.hasRefunded, "LearningPledgePool: lend already refunded");

        uint256 unmatchedAmount = pool.lendSupply - data.settleAmountLend;
        require(unmatchedAmount > 0, "LearningPledgePool: no lend refund");

        // unmatchedAmount       = TOTAL lender money that was NOT used in settlement
        // lendInfo.stakeAmount  = THIS lender's original deposit
        // pool.lendSupply       = TOTAL deposited by ALL lenders
        // refundAmount          = unmatchedAmount * (THIS lender's share of the pool)
        uint256 refundAmount = (unmatchedAmount * lendInfo.stakeAmount) / pool.lendSupply;
        lendInfo.refundAmount += refundAmount;
        lendInfo.hasRefunded = true;

        bool success = IERC20Like(pool.lendToken).transfer(msg.sender, refundAmount);
        require(success, "LearningPledgePool: lend refund transfer failed");

        emit RefundLend(msg.sender, poolId, pool.lendToken, refundAmount);
    }

    function refundBorrow(uint256 poolId) external whenNotPaused poolExists(poolId) stateExecution(poolId) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];
        BorrowInfo storage borrowInfo = userBorrowInfo[msg.sender][poolId];

        require(borrowInfo.stakeAmount > 0, "LearningPledgePool: no borrow stake");
        require(!borrowInfo.hasRefunded, "LearningPledgePool: borrow already refunded");

        uint256 unmatchedAmount = pool.borrowSupply - data.settleAmountBorrow;
        require(unmatchedAmount > 0, "LearningPledgePool: no borrow refund");

        uint256 refundAmount = (unmatchedAmount * borrowInfo.stakeAmount) / pool.borrowSupply;
        borrowInfo.refundAmount += refundAmount;
        borrowInfo.hasRefunded = true;

        bool success = IERC20Like(pool.borrowToken).transfer(msg.sender, refundAmount);
        require(success, "LearningPledgePool: borrow refund transfer failed");

        emit RefundBorrow(msg.sender, poolId, pool.borrowToken, refundAmount);
    }

    function claimLend(uint256 poolId) external whenNotPaused poolExists(poolId) stateExecution(poolId) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];
        LendInfo storage lendInfo = userLendInfo[msg.sender][poolId];

        require(lendInfo.stakeAmount > 0, "LearningPledgePool: no lend stake");
        require(!lendInfo.hasClaimed, "LearningPledgePool: lend already claimed");

        uint256 spAmount = (data.settleAmountLend * lendInfo.stakeAmount) / pool.lendSupply;
        require(spAmount > 0, "LearningPledgePool: no sp claim");

        lendInfo.hasClaimed = true;

        bool success = IDebtTokenLike(pool.spToken).mint(msg.sender, spAmount);
        require(success, "LearningPledgePool: sp mint failed");

        emit ClaimLend(msg.sender, poolId, pool.spToken, spAmount);
    }

    function claimBorrow(uint256 poolId) external whenNotPaused poolExists(poolId) stateExecution(poolId) {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];
        BorrowInfo storage borrowInfo = userBorrowInfo[msg.sender][poolId];

        require(borrowInfo.stakeAmount > 0, "LearningPledgePool: no borrow stake");
        require(!borrowInfo.hasClaimed, "LearningPledgePool: borrow already claimed");

        uint256 jpAmount = (data.settleAmountBorrow * borrowInfo.stakeAmount) / pool.borrowSupply;
        uint256 loanAmount = (data.settleAmountLend * borrowInfo.stakeAmount) / pool.borrowSupply;
        require(jpAmount > 0, "LearningPledgePool: no jp claim");
        require(loanAmount > 0, "LearningPledgePool: no loan claim");

        borrowInfo.hasClaimed = true;

        bool minted = IDebtTokenLike(pool.jpToken).mint(msg.sender, jpAmount);
        require(minted, "LearningPledgePool: jp mint failed");

        bool transferred = IERC20Like(pool.lendToken).transfer(msg.sender, loanAmount);
        require(transferred, "LearningPledgePool: loan transfer failed");

        emit ClaimBorrow(msg.sender, poolId, pool.jpToken, jpAmount, loanAmount);
    }

    function repayPool(uint256 poolId, uint256 maxCollateralAmount)
        external
        onlyOwner
        whenNotPaused
        poolExists(poolId)
        stateExecution(poolId)
        afterEnd(poolId)
    {
        require(dexRouter != address(0), "LearningPledgePool: dex router not set");

        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];
        uint256 requiredRepayment = getRequiredRepayment(poolId);
        uint256 collateralToSell = IDexRouterLike(dexRouter).getAmountIn(
            pool.borrowToken,
            pool.lendToken,
            requiredRepayment
        );

        require(collateralToSell <= maxCollateralAmount, "LearningPledgePool: dex slippage too high");
        require(collateralToSell <= data.settleAmountBorrow, "LearningPledgePool: insufficient collateral");

        // Pool allows DEX to pull collateral tokens from the pool, and sell them for the required repayment amount of lend tokens.
        // So the caller needs to approve the DEX router to spend the collateral tokens first, then the router will pull the collateral and swap them for lend tokens.
        bool approved = IERC20Like(pool.borrowToken).approve(dexRouter, collateralToSell);
        require(approved, "LearningPledgePool: collateral approve failed");

        uint256 soldAmount = IDexRouterLike(dexRouter).swapTokensForExactTokens(
            pool.borrowToken,
            pool.lendToken,
            requiredRepayment,
            maxCollateralAmount,
            address(this)
        );

        data.finishAmountLend = requiredRepayment;
        data.finishAmountBorrow = data.settleAmountBorrow - soldAmount;

        _setPoolState(poolId, PoolState.REPAID);

        emit PoolRepaid(poolId, dexRouter, soldAmount, requiredRepayment, data.finishAmountBorrow);
    }

    function liquidate(uint256 poolId, uint256 maxCollateralAmount)
        external
        onlyOwner
        whenNotPaused
        poolExists(poolId)
        stateExecution(poolId)
    {
        require(dexRouter != address(0), "LearningPledgePool: dex router not set");
        require(isLiquidatable(poolId), "LearningPledgePool: pool not liquidatable");

        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];
        uint256 requiredRepayment = getRequiredRepayment(poolId);
        uint256 collateralToSell = IDexRouterLike(dexRouter).getAmountIn(
            pool.borrowToken,
            pool.lendToken,
            requiredRepayment
        );

        uint256 soldAmount;
        uint256 recoveredAmount;

        if (collateralToSell <= data.settleAmountBorrow) {
            require(collateralToSell <= maxCollateralAmount, "LearningPledgePool: dex slippage too high");

            bool approved = IERC20Like(pool.borrowToken).approve(dexRouter, collateralToSell);
            require(approved, "LearningPledgePool: collateral approve failed");

            soldAmount = IDexRouterLike(dexRouter).swapTokensForExactTokens(
                pool.borrowToken,
                pool.lendToken,
                requiredRepayment,
                maxCollateralAmount,
                address(this)
            );
            recoveredAmount = requiredRepayment;
        } else {
            soldAmount = data.settleAmountBorrow;
            require(soldAmount <= maxCollateralAmount, "LearningPledgePool: dex slippage too high");

            bool approved = IERC20Like(pool.borrowToken).approve(dexRouter, soldAmount);
            require(approved, "LearningPledgePool: collateral approve failed");

            recoveredAmount = IDexRouterLike(dexRouter).swapExactTokensForTokens(
                pool.borrowToken,
                pool.lendToken,
                soldAmount,
                0,
                address(this)
            );
        }

        data.liquidationAmountLend = recoveredAmount;
        data.liquidationAmountBorrow = data.settleAmountBorrow - soldAmount;

        _setPoolState(poolId, PoolState.LIQUIDATION);

        emit PoolLiquidated(poolId, dexRouter, soldAmount, recoveredAmount, data.liquidationAmountBorrow);
    }

    function withdrawLend(uint256 poolId, uint256 spAmount)
        external
        whenNotPaused
        poolExists(poolId)
        stateClosed(poolId)
    {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];

        require(spAmount > 0, "LearningPledgePool: zero sp amount");

        uint256 totalLendAmount =
            pool.state == PoolState.REPAID ? data.finishAmountLend : data.liquidationAmountLend;
        uint256 lendAmount = (totalLendAmount * spAmount) / data.settleAmountLend;

        bool burned = IDebtTokenLike(pool.spToken).burn(msg.sender, spAmount);
        require(burned, "LearningPledgePool: sp burn failed");

        bool transferred = IERC20Like(pool.lendToken).transfer(msg.sender, lendAmount);
        require(transferred, "LearningPledgePool: lend withdraw transfer failed");

        emit WithdrawLend(msg.sender, poolId, spAmount, lendAmount);
    }

    function withdrawBorrow(uint256 poolId, uint256 jpAmount)
        external
        whenNotPaused
        poolExists(poolId)
        stateClosed(poolId)
    {
        PoolBaseInfo storage pool = pools[poolId];
        PoolDataInfo storage data = poolData[poolId];

        require(jpAmount > 0, "LearningPledgePool: zero jp amount");

        uint256 totalCollateralAmount =
            pool.state == PoolState.REPAID ? data.finishAmountBorrow : data.liquidationAmountBorrow;
        uint256 collateralAmount = (totalCollateralAmount * jpAmount) / data.settleAmountBorrow;

        bool burned = IDebtTokenLike(pool.jpToken).burn(msg.sender, jpAmount);
        require(burned, "LearningPledgePool: jp burn failed");

        bool transferred = IERC20Like(pool.borrowToken).transfer(msg.sender, collateralAmount);
        require(transferred, "LearningPledgePool: borrow withdraw transfer failed");

        emit WithdrawBorrow(msg.sender, poolId, jpAmount, collateralAmount);
    }

    function transferOwnership(address newOwner) external onlyOwner {
        require(newOwner != address(0), "LearningPledgePool: zero owner");

        emit OwnershipTransferred(owner, newOwner);
        owner = newOwner;
    }

    function setFeeAddress(address payable newFeeAddress) external onlyOwner {
        require(newFeeAddress != address(0), "LearningPledgePool: zero fee address");

        emit FeeAddressUpdated(feeAddress, newFeeAddress);
        feeAddress = newFeeAddress;
    }

    function setDexRouter(address newDexRouter) external onlyOwner {
        require(newDexRouter != address(0), "LearningPledgePool: zero dex router");

        emit DexRouterUpdated(dexRouter, newDexRouter);
        dexRouter = newDexRouter;
    }

    function setMinLendAmount(uint256 newMinAmount) external onlyOwner {
        emit MinLendAmountUpdated(minLendAmount, newMinAmount);
        minLendAmount = newMinAmount;
    }

    function setMinBorrowAmount(uint256 newMinAmount) external onlyOwner {
        emit MinBorrowAmountUpdated(minBorrowAmount, newMinAmount);
        minBorrowAmount = newMinAmount;
    }

    function setPause(bool paused) external onlyOwner {
        globalPaused = paused;
        emit PauseUpdated(paused);
    }

    modifier whenNotPaused() {
        require(!globalPaused, "LearningPledgePool: paused");
        _;
    }

    modifier poolExists(uint256 poolId) {
        require(poolId < pools.length, "LearningPledgePool: pool not found");
        _;
    }

    modifier stateMatch(uint256 poolId) {
        require(pools[poolId].state == PoolState.MATCH, "LearningPledgePool: pool not match");
        _;
    }

    modifier stateExecution(uint256 poolId) {
        require(pools[poolId].state == PoolState.EXECUTION, "LearningPledgePool: pool not execution");
        _;
    }

    modifier stateClosed(uint256 poolId) {
        require(
            pools[poolId].state == PoolState.REPAID || pools[poolId].state == PoolState.LIQUIDATION,
            "LearningPledgePool: pool not closed"
        );
        _;
    }

    modifier beforeSettle(uint256 poolId) {
        require(block.timestamp < pools[poolId].settleTime, "LearningPledgePool: settle time passed");
        _;
    }

    modifier afterSettle(uint256 poolId) {
        require(block.timestamp >= pools[poolId].settleTime, "LearningPledgePool: before settle time");
        _;
    }

    modifier afterEnd(uint256 poolId) {
        require(block.timestamp >= pools[poolId].endTime, "LearningPledgePool: before end time");
        _;
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "LearningPledgePool: caller is not owner");
        _;
    }

    function _setPoolState(uint256 poolId, PoolState newState) internal {
        PoolState previousState = pools[poolId].state;
        pools[poolId].state = newState;
        emit StateChanged(poolId, previousState, newState);
    }
}
